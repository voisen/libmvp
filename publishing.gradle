apply plugin: 'maven-publish'

publishing {

    publications {
        library(MavenPublication) {
            setGroupId(project.groupId)
            setArtifactId(project.artifactId)
            version project.snapshot
//            from components.java
            afterEvaluate {
                artifact(tasks.getByName("bundleReleaseAar"))
            }
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                def scopes = [configurations.compile]
                if (configurations.hasProperty("api")) {
                    scopes.add(configurations.api)
                }
                if (configurations.hasProperty("implementation")) {
                    scopes.add(configurations.implementation)
                }
                if (configurations.hasProperty("debugImplementation")) {
                    scopes.add(configurations.debugImplementation)
                }
                if (configurations.hasProperty("releaseImplementation")) {
                    scopes.add(configurations.releaseImplementation)
                }
                scopes.each { scope ->
                    scope.allDependencies.each {
                        if (it instanceof ModuleDependency) {
                            boolean isTransitive = ((ModuleDependency) it).transitive
                            if (!isTransitive) {
                                println "<<<< not transitive dependency: [${it.group}, ${it.name}, ${it.version}]"
                                return
                            }
                        }

                        if (it.group == "${project.rootProject.name}.libs" || it.version == 'unspecified') {
                            return
                        }

                        if (it.group && it.name && it.version) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', scope.name)
                        }
                    }
                }
            }
            println "publishing: " + groupId
            println "publishing: " + artifactId
            println "publishing: " + version
        }
    }
}